
defaultTasks 'build'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'maven-publish'

group = 'com.lbayer'
archivesBaseName = 'appup'
version = '0.1.11'

repositories {
    mavenLocal()
    jcenter()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.5'
    testCompile 'junit:junit:4.12'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            def repositoryUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : ''
            def repositoryPassword = project.hasProperty('ossrhPassword') ? ossrhPassword : ''

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: repositoryUsername, password: repositoryPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: repositoryUsername, password: repositoryPassword)
            }

            pom.project {
                name 'Appup Java Bootstrap'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Java application bootstrapping library'
                url 'https://github.com/lfbayer/appup'

                scm {
                    connection 'https://github.com/lfbayer/appup.git'
                    developerConnection 'git@github.com:lfbayer/appup.git'
                    url 'https://github.com/lfbayer/appup'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'lfbayer'
                        name 'Leo Bayer'
                        email 'lfbayer@gmail.com'
                    }
                }
            }
        }
    }
}
